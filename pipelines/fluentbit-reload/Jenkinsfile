@Library('polaris')
import ca.bc.gov.nrids.polaris.Podman
import ca.bc.gov.nrids.polaris.BrokerIntention
import ca.bc.gov.nrids.polaris.Vault

def podman
def intention
def commonModule = [:]

pipeline {
    agent {
        label 'podman'
    }
    environment {
        VAULT_ADDR = "https://vault-iit.apps.silver.devops.gov.bc.ca"
        FB_RUN_USER="wwwsvr"
        FB_S6_SERVICE_HOME = "/apps_ux/s6_services"
        FB_FLUENTBIT_DB = "/apps_data/agents/fluent-bit/fluent-bit-logs.db"
        FB_TAIL_FILES_LIKE = "${params.fluentbitFilesLike}"
        FB_BROKER_URL = "https://nr-broker.apps.silver.devops.gov.bc.ca"
        FB_CONFIG_ROLE_ID = credentials('knox-vault-jenkins-role-id')
        NR_BROKER_JWT = credentials('nr-broker-jwt')
        FB_BUILD_URL = "${env.BUILD_URL}"
        IMAGE_TAG = "${params.imageTag}"
    }
    stages {
        stage('Setup') {
            environment {
                HTTP_PROXY = "http://test-forwardproxy.nrs.bcgov:23128"
                FB_GIT_BRANCH = "${params.fbGitBranch}"
            }
            steps {
                sh 'rm -rf $(pwd)/fb && mkdir $(pwd)/fb'
                script {
                    podman = new Podman(this)
                    podman.run("alpine/git:latest",
                        httpProxy: '\$HTTP_PROXY',
                        options: "--security-opt label=disable -v \$(pwd)/fb:/git -e FB_GIT_BRANCH=${FB_GIT_BRANCH}",
                        command: "clone -b ${FB_GIT_BRANCH} https://github.com/bcgov-nr/nr-funbucks.git .")
                    commonModule = load "pipelines/common.groovy"
                    env.FB_CAUSE_USER_ID = commonModule.getCauseUserId()
                    env.FB_HOST = commonModule.getHost("${params.fluentbitHost}")
                    env.FB_SERVER_OS = commonModule.getServerOS("${params.fluentbitHost}")
                    if (env.FB_SERVER_OS != "linux") {
                        currentBuild.result = 'ABORTED'
                        error('Invalid server OS')
                    }
                    env.FB_BIN_DIR = "${env.FB_SERVER_OS == "linux" ? "/sw_ux/bin" : "E:/sw_nt"}"
                    env.FB_AGENT_ROOT = "${env.FB_SERVER_OS == "linux" ? "/apps_ux/agents" : "E:/apps_nt/agents"}"
                    env.FB_VAULT_CD_USER_FIELD = commonModule.getVaultCdUserField("${params.fluentbitHost}")
                    env.FB_VAULT_CD_PASS_FIELD = commonModule.getVaultCdPassField("${params.fluentbitHost}")
                    env.FB_VAULT_CD_PATH = commonModule.getVaultCdPath("${params.fluentbitHost}")
                }
            }
        }
        stage('Get credentials') {
            steps {
                script {
                    intention = new BrokerIntention(readJSON(file: 'scripts/intention-fb-configure.json'))
                    intention.userId = commonModule.getCauseUserId()
                    intention.eventUrl = env.BUILD_URL
                    intention.open(NR_BROKER_JWT)
                    intention.startAction("login")
                    def vaultToken = intention.provisionToken("login", FB_CONFIG_ROLE_ID)
                    def vault = new Vault(vaultToken)
                    def vault_cd_path = "${env.FB_VAULT_CD_PATH}"
                    def cdCreds = vault.read(vault_cd_path.replaceFirst('/', '/data/'))
                    env.FB_CD_USER = cdCreds[env.FB_VAULT_CD_USER_FIELD]
                    env.FB_CD_PASS = cdCreds[env.FB_VAULT_CD_PASS_FIELD]
                    vault.revokeToken()
                }
            }
        }
        stage('Reload logs') {
            steps {
                script {
                    podman = new Podman(this, null, "artifacts.developer.gov.bc.ca/cc20-gen-docker-local")
                    podman.run("fb-helper:${params.imageTag}",
                        options: "-v \$(pwd)/scripts:/app/scripts -v /sw_ux/bin:/sw_ux/bin -e 'FB_*' -e 'VAULT_*'",
                        command: "scripts/${FB_SERVER_OS}/reload_logs.sh")
                    intention.endAction("login")
                }
            }
        }
    }
    post {
        success {
            script {
                intention.close(true)
            }
        }
        unstable {
            script {
                if (intention) {
                    intention.close(false)
                }
            }
        }
        failure {
            script {
                if (intention) {
                    intention.close(false)
                }
            }
        }
    }
}
